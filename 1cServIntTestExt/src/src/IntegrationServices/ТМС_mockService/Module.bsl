#Область ОбработчикиСобытий

Процедура e1c_ТестовыйПроект_Основной_OfficeToShop_e2eInQueueОбработкаПолученияСообщения(Сообщение, Отказ)
	
	РазмерСообщения = Сообщение.РазмерТела;
	
	МассивПараметров = Новый Массив;
	
	Для Каждого Параметр Из Сообщение.Параметры Цикл
		МассивПараметров.Добавить(Параметр.Ключ + "=" + Строка(Параметр.Значение));
	КонецЦикла;
	
	ПараметрыСтрокой = "";
	ПараметрыСтрокой = СтрСоединить(МассивПараметров, "; ");
	
	Тело = Новый БуферДвоичныхДанных(0);
	Буфер = Новый БуферДвоичныхДанных(РазмерСообщения);
	
	Поток = Сообщение.ПолучитьТелоКакПоток();
	
	Пока Истина Цикл
		Прочитано = Поток.Прочитать(Буфер, 0, РазмерСообщения);
		
		Если Прочитано > 0 Тогда
			Тело = Тело.Соединить(Буфер);
		КонецЕсли;
		
		Если Прочитано < РазмерСообщения Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		ТелоСтрокой = ПолучитьСтрокуИзБуфераДвоичныхДанных(Тело);
		
		ИдентификаторСообщения = Сообщение.Идентификатор;
		
		НаборЗаписей = РегистрыСведений.ТМС_ВходящиеСообщения.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторСообщения.Установить(ИдентификаторСообщения); 
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдентификаторСообщения = ИдентификаторСообщения;
		НоваяЗапись.ИдентификаторСообщенияЗапроса = XMLСтрока(Сообщение.ИдентификаторСообщенияЗапроса);
		НоваяЗапись.Дата = ТекущаяДатаСеанса();
		НоваяЗапись.ДатаОтправки = Сообщение.ДатаОтправки;
		НоваяЗапись.Тело = ТелоСтрокой;
		НоваяЗапись.ДатаУстаревания = Сообщение.ДатаУстаревания;
		НоваяЗапись.КодОтправителя = Сообщение.КодОтправителя;
		НоваяЗапись.КодПолучателя = Сообщение.КодПолучателя;
		НоваяЗапись.РазмерТела = Сообщение.РазмерТела;
		НоваяЗапись.ИдентификаторГруппы = Сообщение.Параметры.Получить("ИдентификаторГруппы");
		НоваяЗапись.Параметры = ПараметрыСтрокой;
		НоваяЗапись.УниверсальнаяДатаВМиллисекундах = ТекущаяУниверсальнаяДатаВМиллисекундах();
		
		НаборЗаписей.Записать();
		
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка, , ,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;

КонецПроцедуры

#КонецОбласти
