////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ СЕРВИСА ИНТЕГРАЦИИ ТМС_mockService
//
// Модуль предназначен для обработки входящих сообщений в рамках тестирования
// интеграционного взаимодействия с mock-сервисом.
//
// Copyright (c) 2025 rzateev
// 
// Этот файл является частью проекта 1cServiceIntegrationMock,
// распространяемого под лицензией MIT.
// Подробности см. в файле LICENSE в корне проекта.
//
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

// Обрабатывает полученное сообщение от внешнего сервиса интеграции.
// Выполняет чтение тела сообщения и сохранение его данных в регистр сведений
// ТМС_ВходящиеСообщения для последующего анализа и тестирования.
//
// Параметры:
//  Сообщение - СообщениеСервисаИнтеграции - Полученное сообщение от внешнего сервиса
//  Отказ - Булево - Признак отказа от обработки сообщения (не используется)
//
Процедура e1c_ТестовыйПроект_Основной_OfficeToShop_e2eInOfficeToOfficeОбработкаПолученияСообщения(Сообщение, Отказ)
	
	// Получение размера тела сообщения
	РазмерСообщения = Сообщение.РазмерТела;
	
	// Формирование строкового представления параметров сообщения
	МассивПараметров = Новый Массив;
	
	Для Каждого Параметр Из Сообщение.Параметры Цикл
		МассивПараметров.Добавить(Параметр.Ключ + "=" + Строка(Параметр.Значение));
	КонецЦикла;
	
	ПараметрыСтрокой = СтрСоединить(МассивПараметров, "; ");
	
	// Чтение тела сообщения из потока
	Тело = Новый БуферДвоичныхДанных(0);
	Буфер = Новый БуферДвоичныхДанных(РазмерСообщения);
	
	Поток = Сообщение.ПолучитьТелоКакПоток();
	
	Пока Истина Цикл
		Прочитано = Поток.Прочитать(Буфер, 0, РазмерСообщения);
		
		Если Прочитано > 0 Тогда
			Тело = Тело.Соединить(Буфер);
		КонецЕсли;
		
		Если Прочитано < РазмерСообщения Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Сохранение данных сообщения в регистр сведений
	Попытка
		ТелоСтрокой = ПолучитьСтрокуИзБуфераДвоичныхДанных(Тело);
		
		ИдентификаторСообщения = Сообщение.Идентификатор;
		
		НаборЗаписей = РегистрыСведений.ТМС_ВходящиеСообщения.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторСообщения.Установить(ИдентификаторСообщения);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИдентификаторСообщения = ИдентификаторСообщения;
		НоваяЗапись.ИдентификаторСообщенияЗапроса = XMLСтрока(Сообщение.ИдентификаторСообщенияЗапроса);
		НоваяЗапись.Дата = ТекущаяДатаСеанса();
		НоваяЗапись.ДатаОтправки = Сообщение.ДатаОтправки;
		НоваяЗапись.Тело = ТелоСтрокой;
		НоваяЗапись.ДатаУстаревания = Сообщение.ДатаУстаревания;
		НоваяЗапись.КодОтправителя = Сообщение.КодОтправителя;
		НоваяЗапись.КодПолучателя = Сообщение.КодПолучателя;
		НоваяЗапись.РазмерТела = Сообщение.РазмерТела;
		НоваяЗапись.ИдентификаторГруппы = Сообщение.Параметры.Получить("ИдентификаторГруппы");
		НоваяЗапись.Параметры = ПараметрыСтрокой;
		НоваяЗапись.УниверсальнаяДатаВМиллисекундах = ТекущаяУниверсальнаяДатаВМиллисекундах();
		
		НаборЗаписей.Записать();
		
	Исключение
		Отказ = Истина;
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации("ТМС_mockService", УровеньЖурналаРегистрации.Ошибка, , ,
			"Ошибка при обработке входящего сообщения: " + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти
